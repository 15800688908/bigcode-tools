import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'maven-publish'

    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'com.jfrog.bintray' version '1.7.3'
}

repositories {
    jcenter()
}

sourceSets {
    libsDirName = 'lib'
}

dependencies {
    compile 'com.github.javaparser:javaparser-core:3.3.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.1'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:slf4j-simple:1.7.25'
    compile 'commons-cli:commons-cli:1.2'

    testCompile 'junit:junit:4.12'
    testCompile 'commons-io:commons-io:2.5'
}

javadoc {
    source = sourceSets.main.allJava
    exclude 'com/tuvistavie/bigcode/astgen/Cli.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = property('bintray.username')
    key = property('bintray.apikey')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'bigcode-ast'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/tuvistavie/bigcode-tools.git'
        publish = true

        version {
            name = '0.1.0'
            desc = 'bigcode-ast first release'
            released  = new Date()
            vcsTag = '0.1.0'
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact javadocJar
            groupId 'com.tuvistavie.bigcode'
            artifactId 'astgen'
            version '0.1.0'
        }
    }
}

test {
    testLogging {
        events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED,
               TestLogEvent.STANDARD_ERROR, TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showCauses true
        showExceptions true
        showStackTraces true
    }
    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', System.getProperty('org.slf4j.simpleLogger.defaultLogLevel', 'WARN')
}

mainClassName = 'com.tuvistavie.bigcode.astgen.Cli'

